include "sciter:reactor.tis";
include "../utils/constants.tis";
include "../utils/utils.tis";
include "Video.tis";
include "Pagination.tis";
include "FormSourceVideo.tis";
include "FormSort.tis";
include "FormSearch.tis";
include "FormGroup.tis";
include "MenuPack.tis";
include "Cross.tis";

class VideosPage: Reactor.Component {
	function this(props) {
		this.props = props || {};

		this.pageSize = 100;
		this.pageNumber = 0;
		this.status = 'Loaded.';
		this.confirmDeletion = true;
	}
	function render() {
		const backend = view.get_info(this.pageSize);
		const nbVideos = backend.nbVideos;
		const nbPages = backend.nbPages;
		const validSize = backend.validSize;
		const validLength = backend.validLength;
		const nbGroups = backend.nbGroups;
		const notFound = backend.notFound;

		const sources = view.get_sources();
		const group_def = view.get_group_def();
		const search_def = view.get_search_def();
		const sorting = view.get_sorting();

		if (this.pageNumber >= nbPages - 1)
			this.pageNumber = nbPages - 1;
		if (this.pageNumber < 0)
			this.pageNumber = 0;

		const sortingIsDefault = sorting.length === 1 && sorting[0] === '-date';
		const app = this;

		return (
			<div id="videos">
				<header>
					<MenuPack title="Options">
						<MenuItem action={Utils.callback(this, this.selectVideos)}>Select videos ...</MenuItem>
						{notFound ? '' : <MenuItem action={Utils.callback(this, this.openRandomVideo)}>Open random video</MenuItem>}
						<MenuItem action={Utils.callback(this, this.reloadDatabase)} >Reload database ...</MenuItem>
						<MenuItem action={Utils.callback(this, this.manageProperties)} >Manage properties</MenuItem>
						<Menu title="Page size ...">
							{[10, 20, 50, 100].map((count, index) => (
								<MenuItemCheck key={index} checked={this.pageSize === count} action={checked => {if (checked) app.setPageSize(count);}} >
									{count} video{count > 1 ? 's' : ''} per page
								</MenuItemCheck>
							))}
						</Menu>
						<MenuItemCheck checked={this.confirmDeletion} action={Utils.callback(this, this.confirmDeletionForNotFound)}>
							confirm deletion for entries not found
						</MenuItemCheck>
					</MenuPack>
					<div class="buttons">
						<button class="group" accesskey="^G">Group videos ...</button>
						<button class="search" accesskey="^F">Search ...</button>
						<button class="sort" accesskey="^S">Sort ...</button>
					</div>
				</header>
				<div class="horizontal central-panel">
					<div class="side-panel">
						<div>
							<div class="status">
								Displaying {group_def || search_def ? <em>filtered</em> : <strong>all</strong>} videos
							</div>
							<div class="status sources">
								<div class="sources">Sources:</div>
								{sources.map((source, index) => <div class="source" key={index}>{source.join('.')}</div>)}
							</div>
							{group_def ? (
							<div class="status">
								<div>Grouped by <Cross title="reset group" action={Utils.callback(this, this.resetGroup)}/></div>
								<div>{FIELD_TITLES[group_def.field]}{' '}{group_def.reverse ? (<span>&#9660;</span>) : (<span>&#9650;</span>)}</div>
							</div>
							) : ''}
							{search_def ? (
							<div class="status">
								<div>Searched {SEARCH_TYPE_TITLE[search_def.cond]} <Cross title="reset search" action={Utils.callback(this, this.resetSearch)}/></div>
								<div>&quot;<strong>{search_def.text}</strong>&quot;</div>
							</div>
							) : ''}
							<div class="status">
								<div>Sorted by {sortingIsDefault ? '' : <Cross title="reset sorting" action={Utils.callback(this, this.resetSort)} />}</div>
								{sorting.map((val, i) => <div key={i}><strong>{val.substr(1)}</strong>{' '}{val[0] == '-' ? (<span>&#9660;</span>) : (<span>&#9650;</span>)}</div>)}
							</div>
						</div>
					</div>
					<div class="main-panel">
						<div class="status pagination">
							{group_def ? (
								<Pagination singular="group" plural="groups" nbPages={group_def.nb_groups} pageNumber={group_def.group_id} onChange={Utils.callback(this, this.changeGroup)}/>
							) : ''}
							<Pagination singular="page" plural="pages" nbPages={nbPages} pageNumber={this.pageNumber} onChange={Utils.callback(this, this.changePage)}/>
						</div>
						{(group_def && group_def.nb_groups) ? (<div class="status">{Utils.sentence(FIELD_TITLES[group_def.field])}:  <strong>{view.get_group_field_value()}</strong></div>) : ''}
						<div class="videos">{this.getVideos()}</div>
					</div>
				</div>
				<footer>
					<div id="status">{this.status}</div>
					<div class="infos">
							<span class="info count">{nbVideos} video{nbVideos > 1 ? 's' : ''}</span>
							<span class="info size">{validSize}</span>
							<span class="info length">{validLength}</span>
					</div>
					<div>
						<div class="select-videos" accesskey="^V"/>
					</div>
				</footer>
			</div>
		);
	}

	function resetView(videoShift) {
		const container = $(div.videos);
		container.scrollTo(0, 0);
		if (container.length)
			container[videoShift || 0].scrollToView(true);
	}
	function updateView(state, videoShift = undefined) {
		this.update(state);
		this.post(() => this.resetView(videoShift));
	}
	function getVideos() {
		const bounds = view.get_videos_index_range(this.pageSize, this.pageNumber);
		const start = bounds[0];
		const end = bounds[1];
		const videos = [];
		const parent = this;
		for (let i = start; i < end; ++i) {
			const data = view.get_video_fields(i, FIELDS);
			videos.push(<Video key={data.video_id} data={data} index={i} parent={parent} confirmDeletion={this.confirmDeletion}/>);
		}
		return videos;
	}
	function updateStatus(status) {
		//
		this.update({status: status});
	}
	function updateGroup(groupNumber) {
		view.set_group(groupNumber);
		this.updateView({pageNumber: 0});
	}
	function selectVideos() {
		const app = this;
		const tree = view.get_source_tree();
		const sources = view.get_sources();
		this.props.app.loadDialog('Select Videos', onClose => (
			<FormSourceVideo tree={tree} sources={sources} onClose={sources => {
				onClose();
				if (sources && sources.length) {
					view.set_sources(sources);
					app.update({pageNumber: 0});
				}
			}} />
		));
	}
	function openRandomVideo() {
		const filename = view.open_random_video(this.pageSize);
		this.updateView({status: 'Randomly opened: ' + filename});
	}
	function reloadDatabase() {
		//
		this.props.app.loadPage('home', {update: true});
	}
	function manageProperties() {
		//
		this.props.app.loadPage('properties');
	}
	function confirmDeletionForNotFound(value) {
		//
        this.update({confirmDeletion: value});
	}
	function setPageSize(value) {
        if (value !== this.pageSize) {
            this.updateView({pageSize: value, pageNumber: 0});
        }
	}
	function changePage(pageNumber) {
		this.updateView({pageNumber: pageNumber});
	}
	function changeGroup(groupNumber) {
		this.updateGroup(groupNumber);
	}
	function resetSearch() {
		view.set_search(null, null);
		this.updateView({pageNumber: 0});
	}
	function resetSort() {
		view.set_sorting([]);
		this.updateView({pageNumber: 0});
	}
	function resetGroup() {
		view.group_videos(null, null);
		this.update({pageNumber: 0});
	}

	event focus $(.select-videos) (evt, element) {
		stdout.println('FOCUS');
	}
	event click $(button#reset-search) (evt, element) {
		//
		this.resetSearch();
	}
	event click $(button#reset-sort) (evt, element) {
		//
		this.resetSort();
	}
	event click $(button#reset-group) (evt, element) {
		//
		this.resetGroup();
	}
	event click $(button.sort) (evt, element) {
		const app = this;
		const sorting = view.get_sorting();
		this.props.app.loadDialog('Sort videos', onClose => (
			<FormSort sorting={sorting} onClose={sorting => {
				onClose();
				if (sorting && sorting.length) {
					view.set_sorting(sorting);
					app.updateView({pageNumber: 0});
				}
			}}/>
		));
    }
    event click $(button.search) (evt, element) {
        const app = this;
        const search_def = view.get_search_def() || {text: null, cond: null};
        this.props.app.loadDialog('Search videos', onClose => (
            <FormSearch text={search_def.text} cond={search_def.cond} onClose={criterion => {
                onClose();
				if (criterion && criterion.text.length && criterion.cond.length) {
					view.set_search(criterion.text, criterion.cond);
					app.updateView({pageNumber: 0});
				}
            }} />
        ));
    }
    event click $(button.group) (evt, element) {
        const app = this;
        const group_def = view.get_group_def() || {field: null, reverse: null};
        this.props.app.loadDialog('Group videos with same:', onClose => (
            <FormGroup field={group_def.field} reverse={group_def.reverse} onClose={criterion => {
                onClose();
		        if (criterion) {
		            view.group_videos(criterion.field, criterion.reverse);
					this.updateView({pageNumber: 0});
		        }
            }} />
        ));
    }
    event click $(#status) {
		//
		this.updateStatus('Ready.');
	}
}