include "sciter:reactor.tis";
include "../utils/constants.tis";
include "../utils/utils.tis";
include "Video.tis";
include "Pagination.tis";
include "FormSourceVideo.tis";
include "FormSort.tis";
include "FormSearch.tis";
include "FormGroup.tis";

class VideosPage: Reactor.Component {
	function this(props, children) {
		this.props = props || {};
		this.children = children || {};

		this.pageSize = 100;
		this.pageNumber = 0;
		this.status = 'Loaded.';
		this.confirmDeletion = true;
	}
	function render() {
		const backend = view.get_info(this.pageSize);
		const nbVideos = backend.nbVideos;
		const nbPages = backend.nbPages;
		const validSize = backend.validSize;
		const validLength = backend.validLength;
		const nbGroups = backend.nbGroups;

		const sources = view.get_sources();
		const group_def = view.get_group_def();
		const search_def = view.get_search_def();
		const sorting = view.get_sorting();

		if (this.pageNumber >= nbPages - 1)
			this.pageNumber = nbPages - 1;
		if (this.pageNumber < 0)
			this.pageNumber = 0;

		const app = this;
		const onChangePage = function(pageNumber) {
			app.updateView({pageNumber: pageNumber});
		};
		const onChangeGroup = function(groupNumber) {
			app.updateGroup(groupNumber);
		};

		return (
			<div id="videos">
				<div class="row central-panel">
					<div class="col side-panel">
						<div class="status">
							Displaying {group_def || search_def ? <em>filtered</em> : <strong>all</strong>} videos
						</div>
						<div class="status sources">
						<div class="sources">Sources:</div>
						{sources.map((source, index) => <div class="source" key={index}>{source.join('.')}</div>)}
						</div>
						<hr/>

						<div class="status">
							<button class="plain" id="source">Select videos ...</button>
						</div>
						<hr/>

						{group_def ? (
							<div>
								<div class="status">
									<div>Videos grouped by</div>
									<div>{FIELD_TITLES[group_def.field]}{' '}{group_def.reverse ? (<span>&#9660;</span>) : (<span>&#9650;</span>)}</div>
									<div>Group {group_def.nb_groups ? group_def.group_id + 1 : 0} / {group_def.nb_groups}</div>
								</div>
								<div class="buttons-line">
									<button class="edit group">edit group ...</button>
									<button class="reset" id="reset-group">reset group</button>
								</div>
							</div>
						) : (
							<div class="status">
								<button class="group plain">Group videos ...</button>
							</div>
						)}
						<hr/>

						{search_def ? (
							<div>
								<div class="status">
									<div>Searched</div>
									<div>&quot;<strong>{search_def.text}</strong>&quot;</div>
									<div>({SEARCH_TYPE_TITLE[search_def.cond]})</div>
								</div>
								<div class="buttons-line">
									<button class="edit search" accesskey="^F">search ...</button>
									<button class="reset" id="reset-search">reset search</button>
								</div>
							</div>
						) : (
							<div class="status">
								<button class="search plain" accesskey="^F">Search ...</button>
							</div>
						)}
						<hr/>

						{sorting.length ? (
							<div>
								<div class="status">
									<div>Videos sorted by</div>
									{sorting.map((val, i) => <div key={i}><strong>{val.substr(1)}</strong>{' '}{val[0] == '-' ? (<span>&#9660;</span>) : (<span>&#9650;</span>)}</div>)}
								</div>
								<div class="buttons-line status">
									<button class="edit sort">edit sorting ...</button>
									<button class="reset" id="reset-sort">reset sorting</button>
								</div>
							</div>
						) : (
							<div class="status">
								<button class="sort plain">Sort ...</button>
							</div>
						)}
						<hr/>

						<div class="status">
							<select class="select-page-size plain" novalue="videos per page" :value={this.pageSize}>
								{[10, 20, 50, 100].map((count, index) => <option key={index} value={count}>Display {count} video{count > 1 ? 's' : ''} per page</option>)}
							</select>
						</div>
						<hr/>
						<div class="status">
							<button class="plain" id="open-random-file">open a random video</button>
						</div>
						<hr/>
						<div class="status">
							<button class="plain" id="reload">reload database</button>
						</div>
						<hr/>
						<div class="status">
							<button class="plain" id="manage-properties">manage properties</button>
						</div>
						<hr/>
						<div class="status">
							<input type="checkbox" name="confirm-deletion" id="confirm-deletion" :checked={this.confirmDeletion}/>
							{' '}
							<label for="confirm-deletion">confirm deletion for entries not found</label>
						</div>
					</div>
					<div class="col main-panel">
						<div class="status pagination">
							{group_def ? (
								<Pagination singular="group" plural="groups" nbPages={group_def.nb_groups} pageNumber={group_def.group_id} onChange={onChangeGroup}/>
							) : ''}
							<Pagination singular="page" plural="pages" nbPages={nbPages} pageNumber={this.pageNumber} onChange={onChangePage}/>
						</div>
						{(group_def && group_def.nb_groups) ? (<div class="status">{Utils.sentence(FIELD_TITLES[group_def.field])}:  <strong>{view.get_group_field_value()}</strong></div>) : ''}
						<div class="videos">{this.getVideos()}</div>
					</div>
				</div>
				<footer>
					<div id="status">{this.status}</div>
					<div class="infos">
							<span class="info count">{nbVideos} video{nbVideos > 1 ? 's' : ''}</span>
							<span class="info size">{validSize}</span>
							<span class="info length">{validLength}</span>
					</div>
				</footer>
			</div>
		);
	}

	function resetView(videoShift) {
		const container = $(div.videos);
		container.scrollTo(0, 0);
		if (container.length)
			container[videoShift || 0].scrollToView(true);
	}
	function updateView(state, videoShift = undefined) {
		this.update(state);
		this.post(() => this.resetView(videoShift));
	}
	function getVideos() {
		const bounds = view.get_videos_index_range(this.pageSize, this.pageNumber);
		const start = bounds[0];
		const end = bounds[1];
		const videos = [];
		const parent = this;
		for (let i = start; i < end; ++i) {
			const data = view.get_video_fields(i, FIELDS);
			videos.push(<Video key={data.video_id} data={data} index={i} parent={parent} confirmDeletion={this.confirmDeletion}/>);
		}
		return videos;
	}
	function updateStatus(status) {
		//
		this.update({status: status});
	}
	function updateGroup(groupNumber) {
		view.set_group(groupNumber);
		this.updateView({pageNumber: 0});
	}
	event click $(button#open-random-file) (evt, element) {
		element.state.disabled = true;
		const info = view.open_random_video(this.pageSize);
		this.updateView({pageNumber: info[0], status: 'Randomly opened: ' + info[2]}, info[1]);
		this.post(() => element.state.disabled = false);
	}
	event click $(button#reload) (evt, element) {
		this.props.app.loadPage('home', {update: true});
	}
	event click $(button#manage-properties) (evt, element) {
		this.props.app.loadPage('properties');
	}
	event click $(button#reset-search) (evt, element) {
		view.set_search(null, null);
		this.updateView({pageNumber: 0});
	}
	event click $(button#reset-sort) (evt, element) {
		view.set_sorting([]);
		this.updateView({pageNumber: 0});
	}
	event click $(button#reset-group) (evt, element) {
		view.group_videos(null, null);
		this.update({pageNumber: 0});
	}
	event click $(button#source) (evt, element) {
		const app = this;
		const tree = view.get_source_tree();
		const sources = view.get_sources();
		this.props.app.loadDialog('Select Videos', onClose => (
			<FormSourceVideo tree={tree} sources={sources} onClose={sources => {
				onClose();
				if (sources && sources.length) {
					view.set_sources(sources);
					app.update({pageNumber: 0});
				}
			}} />
		));
	}
	event click $(button.sort) (evt, element) {
		const app = this;
		const sorting = view.get_sorting();
		this.props.app.loadDialog('Sort videos', onClose => (
			<FormSort sorting={sorting} onClose={sorting => {
				onClose();
				if (sorting && sorting.length) {
					view.set_sorting(sorting);
					app.updateView({pageNumber: 0});
				}
			}}/>
		));
    }
    event click $(button.search) (evt, element) {
        const app = this;
        const search_def = view.get_search_def() || {text: null, cond: null};
        this.props.app.loadDialog('Search videos', onClose => (
            <FormSearch text={search_def.text} cond={search_def.cond} onClose={criterion => {
                onClose();
				if (criterion && criterion.text.length && criterion.cond.length) {
					view.set_search(criterion.text, criterion.cond);
					app.updateView({pageNumber: 0});
				}
            }} />
        ));
    }
    event click $(button.group) (evt, element) {
        const app = this;
        const group_def = view.get_group_def() || {field: null, reverse: null};
        this.props.app.loadDialog('Group videos with same:', onClose => (
            <FormGroup field={group_def.field} reverse={group_def.reverse} onClose={criterion => {
                onClose();
		        if (criterion) {
		            view.group_videos(criterion.field, criterion.reverse);
					this.updateView({pageNumber: 0});
		        }
            }} />
        ));
    }
    event change $(select.select-page-size) (evt, element) {
        const value = element.value;
        if (value !== this.pageSize) {
            this.updateView({pageSize: value, pageNumber: 0});
        }
    }
    event change $(input[name=confirm-deletion]) (evt, element) {
        this.update({confirmDeletion: element.value});
    }
    event click $(#status) {
		//
		this.updateStatus('Ready.');
	}
}